#!/bin/zsh

help() {
    cat << EOF | cut -c 9-
        Usage: dotfiles OPTION
        Deploy dotfiles from a repository to ~/.*

            help                        display this help text and exit
            install topic [topic...]    symlink topic/**/*.symlink files to ~
EOF
}

install() {
    # basedir is root of dotfiles.git repository (assumes this script is there)
    pushd $( dirname $0 ) > /dev/null
    basedir=$( pwd )
    popd > /dev/null

    topic=$1
    if [[ ! -d ${basedir}/${topic} ]]; then
        echo "${basedir}/${topic} does not exist"
    else
        echo "Installing ${topic}..."
        for x in ${basedir}/${topic}/**/*.symlink(N); do
            if [[ -a ${HOME}/.${x:t:r} ]]; then
                echo "- Ignoring ${x#${basedir}/}: ${HOME}/.${x:t:r} already exists"
            else
                echo "- Creating symlink for ${x#${basedir}/}"
                ln -s ${x} ${HOME}/.${x:t:r}
            fi
        done
    fi
}

case $1 in
    help)
        help
        exit 0
        ;;
    install)
        if [[ $# -lt 2 ]]; then
            echo "Not enough in hashtag"
        else
            for topic in ${*[2,${#*}]}; do
                install $topic
            done
        fi
        exit 0
        ;;
    *)
        help
        exit 1
        ;;
esac
